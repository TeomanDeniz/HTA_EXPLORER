'╔═══════════════════════════════════════╦═════════════════════════════╗
'║ VBS - HTA_EXPLORER [IE 9+]            ║       Maximum Tension       ║
'╠═══════════════════════════════════════╬═════════════════════════════╣
'║                                       │      ▄▄▄            ▄▄▄     ║
'║ sintrode & Teoman Deniz               │  ░    ░▒▓▒▄▄    ▄▄▒▓▒░    ░ ║
'║ maximum-tension.com                   │  ░░                      ░░ ║
'║                                       │  ░▒░    ░ ░░ ░ ░  ░  ░░░░▒░ ║
'║ ╔───┬──────────────────╗              │   ░▒░░ ░▒░▒▓░▒░▒░░▓░░░▒▒▒░  ║
'║ │ © │ Maximum Tension  │              │   ░░▒░░▒▒▓██▓█▓█▒░▒▓▓▒▒░░   ║
'║ ├───┴─────┬────────────┤ ┌────────────┤   ░▒▓▒▒▓▓██████████▓▓▒▒░    ║
'║ │ License │ MIT        │ │ 2022/11/15 │    ░░░░▒▒▒▓▒▒▓▒▒▒▓▒▒▒░░     ║
'║ ╚─────────┴────────────╝ │ YYYY/MM/DD │       ░░░░▒░░▒░░░▒░░░░      ║
'╚══════════════════════════╩════════════╩═════════════════════════════╝

Dim MT_SHELL : Set MT_SHELL = CreateObject("WScript.Shell") 'GET WINDOWS SHELL EQUIPMENTS'
Set window.onload = GetRef("VB_MAIN") 'IF VBSCRIPT NOT AUTOMATICALLY WORKED, RUN WITH "VB_MAIN" SUB'

Sub VB_MAIN()
	EXPLORER_INDEX.value = 1
	MT_EXPLORER_OPEN_FOLDER(MT_SHELL.CurrentDirectory) 'START EXPLORER WITH CURRENT HTA DIRECTORY'
End Sub

Sub MT_EXPLORER_OPEN_FOLDER(MT_DIR_PARM)
	Dim EXPLORER_SHOW_HIDDEN_OBJECTS
	Dim MT_EXPLORER_OBJECT_NO
	Dim MT_EXPLORER_ENTITIES
	Dim MT_EXPLORER_FOLDERS
	Dim MT_EXPLORER_FILES
	Dim MT_EXPLORER_BACK
	Dim MT_EXPLORER
	Dim MT_TEMP
	Dim T

	EXPLORER_SHOW_HIDDEN_OBJECTS = 0 'SHOW HIDDEN/SYSTEM FILES/FOLDERS?'
	EXPLORER_PATH.value = MT_DIR_PARM 'GET ARGUMENT (DIRECTORY)'
	Set MT_EXPLORER = document.getElementById("MT_EXPLORER") 'GET FILE/FOLDER TABLE ELEMENT'
	Set MT_EXPLORER_BACK = document.getElementById("MT_EXPLORER_BACK") 'GET "BACK" BUTTON ELEMENT'
	Set MT_EXPLORER_ENTITIES = CreateObject("Scripting.FileSystemObject").GetFolder(MT_DIR_PARM) 'GET ALL OBJECTS'
	Set MT_EXPLORER_FILES = MT_EXPLORER_ENTITIES.Files 'GET ONLY FILES OBJECTS'
	Set MT_EXPLORER_FOLDERS = MT_EXPLORER_ENTITIES.SubFolders 'GET ONLY FOLDERS OBJECTS'
	MT_EXPLORER.innerHTML = "" 'CLEAR THE OBJECT TABLE FOR ADD NEW OBJECTS ON IT'
	MT_EXPLORER_OBJECT_NO = 1 'RESET OBJECT NUMBERS'
	if EXPLORER_PATH.value<>"C:\Users" Then 'CHECK IF NOT THE DIRECTORY AT IT IS LIMIT'
		MT_EXPLORER_BACK.style.display="BLOCK" 'ACTIVE BACK BUTTON VIA SHOW IT'
		MT_EXPLORER_BACK_NOT.style.display="NONE" 'HIDE DISABLED BUTTON'
	End If
	For Each EXPLORER_FOLDER in MT_EXPLORER_FOLDERS 'COUNT EACH FOLDER OBJECT'
		T = "" 'RESET TMP'
		If Not (((EXPLORER_FOLDER.Attributes And 38) Or (EXPLORER_FOLDER.Attributes And 6)) And (EXPLORER_SHOW_HIDDEN_OBJECTS = 0)) Then 'CHECK IF FOLDER IS HIDDEN OR SYSTEM FOLDER AND "EXPLORER_SHOW_HIDDEN_OBJECTS" IS ENABLED'
			MT_TEMP = Replace(EXPLORER_FOLDER.Path, "\", "\\") 'REPLACE "\" WITH "\\" FOR SPECIAL CHARACTER IN HTML ARGUMENT CONTENT'
			T = T & "<DIV CLASS='EXPLORER_ENTITY_HITBOX'>"
			T = T & 	"<DIV ONKEYPRESS='MT_CHECK_ENTER_FOLDER(""" & MT_TEMP & """)'"
			T = T & 		" " & "CLASS='EXPLORER_FOLDER'"
			T = T & 		" " & "TABINDEX='1'"
			T = T & 		" " & "ONDBLCLICK='MT_EXPLORER_OPEN_FOLDER(""" & MT_TEMP & """)'"
			T = T & 		" " & "ONFOCUS='MT_EXPLORER_UPDATE_FOCUS(""" & MT_EXPLORER_OBJECT_NO & """)'>"
			T = T & 	"<SPAN>" & EXPLORER_FOLDER.Name & "</SPAN>"
			T = T & 	"</DIV>"
			T = T & "</DIV>"
			MT_EXPLORER.innerHTML = MT_EXPLORER.innerHTML & T
			MT_EXPLORER_OBJECT_NO = MT_EXPLORER_OBJECT_NO + 1
		End If
	Next
	For Each EXPLORER_FILE in MT_EXPLORER_FILES 'COUNT EACH FILE OBJECT'
		T = "" 'RESET TMP'
		If Not (((EXPLORER_FILE.Attributes = 38) Or (EXPLORER_FILE.Attributes = 6)) And (EXPLORER_SHOW_HIDDEN_OBJECTS = 0)) Then 'CHECK IF FILE IS HIDDEN OR SYSTEM FILE AND "EXPLORER_SHOW_HIDDEN_OBJECTS" IS ENABLED'
			MT_TEMP = Replace(EXPLORER_FILE.Path, "\", "\\") 'REPLACE "\" WITH "\\" FOR SPECIAL CHARACTER IN HTML ARGUMENT CONTENT'
			T = T & "<DIV CLASS='EXPLORER_ENTITY_HITBOX'>"
			T = T & 	"<DIV ONKEYPRESS='MT_CHECK_ENTER_FILE(""" & MT_TEMP & """)'"
			T = T & 		" " & "CLASS='EXPLORER_FILE'"
			T = T & 		" " & "TABINDEX='1'"
			T = T & 		" " & "ONDBLCLICK='MT_EXPLORER_OPEN_FILE(""" & MT_TEMP & """)'"
			T = T & 		" " & "FILE_ICON='" & MT_TEMP & "'"
			T = T & 		" " & "ONFOCUS='MT_EXPLORER_UPDATE_FOCUS(""" & MT_EXPLORER_OBJECT_NO & """)'>"
			T = T & 	"<SPAN>" & EXPLORER_FILE.Name & "</SPAN>"
			T = T & 	"</DIV>"
			T = T & "</DIV>"
			MT_EXPLORER.innerHTML = MT_EXPLORER.innerHTML & T
			MT_EXPLORER_OBJECT_NO = MT_EXPLORER_OBJECT_NO + 1
		End If
	Next
End Sub

Sub MT_EXPLORER_OPEN_FILE(MT_PATH)
	MT_PATH = chr(34) & MT_PATH & chr(34)
	MT_SHELL.Run(MT_PATH)
End Sub

Sub MT_EXPLORER_BACK_DIRECTORY()
	Dim LAST_PATH
	Dim MT_BACK_DIR

	LAST_PATH = InStrRev(EXPLORER_PATH.value, "\")
	MT_BACK_DIR = Mid(EXPLORER_PATH.value, LAST_PATH)
	MT_BACK_DIR = Replace(EXPLORER_PATH.value, MT_BACK_DIR, "")
	If MT_BACK_DIR="C:\Users" Then
		MT_EXPLORER_BACK.style.display="NONE"
		MT_EXPLORER_BACK_NOT.style.display="BLOCK"
	End If
	MT_EXPLORER_OPEN_FOLDER(MT_BACK_DIR)
End Sub

Sub MT_EXPLORER_CHECK_KEY()
	Dim MT_EXPLORER_TAB_FOR_LOOP_KEYS
	Dim MT_EXPLORER_TAB_FOR_CALCULATE
	Dim MT_EXPLORER_FOCUSED_OBJECT
	Dim MT_EXPLORER_IF_CALCULATOR1
	Dim MT_EXPLORER_IF_CALCULATOR2
	Dim MT_EXPLORER_IF_CALCULATOR3
	Dim MT_EXPLORER_IF_CALCULATOR4
	Dim MT_EXPLORER_OBJECT_WIDTH
	Dim MT_EXPLORER_TAB_FOR_LOOP
	Dim MT_EXPLORER_REST_OBJECTS
	Dim MT_EXPLORER_SPACE_WIDTH
	Dim MT_EXPLORER_OBJECTS

	MT_EXPLORER_TAB_FOR_LOOP_KEYS = "" 'READY THE VARIABLE TO INCOMING FOR LOOP'
	MT_EXPLORER_TAB_FOR_LOOP = 0 'READY THE VARIABLE TO INCOMING FOR LOOP'
	MT_EXPLORER_SPACE_WIDTH = 4 'SPACE SIZE BETWEEN OBJECTS'
	MT_EXPLORER_OBJECT_WIDTH = 72 + MT_EXPLORER_SPACE_WIDTH 'OBJECT SIZE ITSELF (72PX) + SPACE SIZE'
	MT_EXPLORER_FOCUSED_OBJECT = EXPLORER_INDEX.value 'GET WHICH OBJECT IS FOCUSED'
	'         5,723684 (6)                   435PX                        76           '
	MT_EXPLORER_TAB_FOR_CALCULATE = MT_EXPLORER.clientWidth \ MT_EXPLORER_OBJECT_WIDTH 'MAX OBJECT NUMBER IN A LINE' 'USED "\" FOR INTEGER!'
	Set MT_EXPLORER_OBJECTS = document.querySelectorAll(".EXPLORER_ENTITY_HITBOX") 'GET TOTAL NUMBER OF OBJECTS'
	MT_EXPLORER_REST_OBJECTS = CInt(MT_EXPLORER_OBJECTS.length) Mod CInt(MT_EXPLORER_TAB_FOR_CALCULATE) 'GET THE NUMBER OF LAST FILES'

	'╔═════════════════════════════════════════════════════════════════════════════════════════╗
	'║                           PRETEND SOME VARIABLES FOR (IF) SEGMENT                       ║
	'╠═════════════════════════════════════════════════════════════════════════════════════════╣
	  MT_EXPLORER_IF_CALCULATOR1 = MT_EXPLORER_OBJECTS.length - MT_EXPLORER_TAB_FOR_CALCULATE '║
	  MT_EXPLORER_IF_CALCULATOR2 = MT_EXPLORER_OBJECTS.length - MT_EXPLORER_REST_OBJECTS      '║
	  MT_EXPLORER_IF_CALCULATOR3 = MT_EXPLORER_TAB_FOR_CALCULATE + MT_EXPLORER_REST_OBJECTS   '║
	  MT_EXPLORER_IF_CALCULATOR4 = MT_EXPLORER_TAB_FOR_CALCULATE - MT_EXPLORER_REST_OBJECTS   '║
	  MT_EXPLORER_IF_CALCULATOR5 = MT_EXPLORER_TAB_FOR_CALCULATE - MT_EXPLORER_IF_CALCULATOR4 '║
	  MT_EXPLORER_IF_CALCULATOR6 = MT_EXPLORER_TAB_FOR_CALCULATE + MT_EXPLORER_IF_CALCULATOR4 '║
	'╚═════════════════════════════════════════════════════════════════════════════════════════╝

	'╔═════════════════════════════════════════════════════════════════════╗
	'║                     CONVERT VARIABLES TO INTEGER                    ║
	'╠═════════════════════════════════════════════════════════════════════╣
	  MT_EXPLORER_TAB_FOR_CALCULATE = CInt(MT_EXPLORER_TAB_FOR_CALCULATE) '║
	  MT_EXPLORER_IF_CALCULATOR1    = CInt(MT_EXPLORER_IF_CALCULATOR1)    '║
	  MT_EXPLORER_IF_CALCULATOR2    = CInt(MT_EXPLORER_IF_CALCULATOR2)    '║
	  MT_EXPLORER_IF_CALCULATOR3    = CInt(MT_EXPLORER_IF_CALCULATOR3)    '║
	  MT_EXPLORER_IF_CALCULATOR5    = CInt(MT_EXPLORER_IF_CALCULATOR5)    '║
	  MT_EXPLORER_IF_CALCULATOR6    = CInt(MT_EXPLORER_IF_CALCULATOR6)    '║
	  MT_EXPLORER_FOCUSED_OBJECT    = CInt(MT_EXPLORER_FOCUSED_OBJECT)    '║
	  MT_EXPLORER_REST_OBJECTS      = CInt(MT_EXPLORER_REST_OBJECTS)      '║
	'╚═════════════════════════════════════════════════════════════════════╝
	If window.event.keyCode = 27 Then self.close() 'ESC' 'CLOSE PROGRAM'
	If window.event.keyCode = 8  Then MT_EXPLORER_BACK_DIRECTORY() 'BACKSPACE <--' 'GO BACK DIRECTORY'
	If window.event.keyCode = 39 Then MT_SHELL.SendKeys "{TAB}" '→' 'ONE FOCUS RIGHT'
	If window.event.keyCode = 37 Then MT_SHELL.SendKeys "+{TAB}" '←' 'ONE FOCUST LEFT'
	If window.event.keyCode = 38 Then '↑' 'ONE FOCUS UP'
		If MT_EXPLORER_FOCUSED_OBJECT < MT_EXPLORER_REST_OBJECTS + 1 Then
			Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_REST_OBJECTS
				MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "+{TAB}"
				MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
			Loop
		Else
			If MT_EXPLORER_FOCUSED_OBJECT < MT_EXPLORER_TAB_FOR_CALCULATE + 1 Then
				Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_IF_CALCULATOR3
					MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "+{TAB}"
					MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
				Loop
			Else
				Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_TAB_FOR_CALCULATE
					MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "+{TAB}"
					MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
				Loop
			End IF
		End IF
		MT_SHELL.SendKeys MT_EXPLORER_TAB_FOR_LOOP_KEYS 'SEND ALL KEYS'
	End IF
	If window.event.keyCode = 40 Then '↓'
		If MT_EXPLORER_FOCUSED_OBJECT > MT_EXPLORER_IF_CALCULATOR1 Then 'IF FOCUSED OBJECT AT THE LIMIT'
			If MT_EXPLORER_FOCUSED_OBJECT > MT_EXPLORER_IF_CALCULATOR2 Then 'IF FOCUSED OBJECT IS IN LAST LINE'
				Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_REST_OBJECTS
					MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "{TAB}"
					MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
				Loop
			Else
				Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_IF_CALCULATOR3
					MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "{TAB}"
					MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
				Loop
			End IF
		Else
			Do While MT_EXPLORER_TAB_FOR_LOOP < MT_EXPLORER_TAB_FOR_CALCULATE
				MT_EXPLORER_TAB_FOR_LOOP_KEYS = MT_EXPLORER_TAB_FOR_LOOP_KEYS + "{TAB}"
				MT_EXPLORER_TAB_FOR_LOOP = MT_EXPLORER_TAB_FOR_LOOP + 1
			Loop
		End IF
		MT_SHELL.SendKeys MT_EXPLORER_TAB_FOR_LOOP_KEYS 'SEND ALL KEYS'
	End If
	If window.event.keyCode = 116 Then MT_EXPLORER_OPEN_FOLDER(EXPLORER_PATH.value)
End Sub

Sub MT_CHECK_ENTER_FOLDER(MT_PATH_CEFO)
	If window.event.keyCode = 13 Then MT_EXPLORER_OPEN_FOLDER(MT_PATH_CEFO)
End Sub

Sub MT_CHECK_ENTER_FILE(MT_PATH_CEFI)
	If window.event.keyCode = 13 Then MT_EXPLORER_OPEN_FILE(MT_PATH_CEFI)
End Sub

Sub MT_EXPLORER_UPDATE_FOCUS(MT_OBJECT_NUMBER)
	EXPLORER_INDEX.value = MT_OBJECT_NUMBER
End Sub